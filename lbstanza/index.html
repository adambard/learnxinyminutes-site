<!DOCTYPE html>
<html lang="en">
  <head>
    <script async src="https://www.googletagmanager.com/gtag/js?id=G-WXC8R75DEN"></script>
    <script>
      window.dataLayer = window.dataLayer || [];
      function gtag(){dataLayer.push(arguments);}
      gtag('js', new Date());

      gtag('config', 'G-WXC8R75DEN');
    </script>

    <meta charset="utf-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1">
    <meta http-equiv="Content-Language" content="en">
    <title>Learn LB Stanza in Y Minutes</title>
    <meta name="description" content="">
    <meta name="viewport" content="width=device-width, initial-scale=1">

    <link rel="preconnect" href="https://fonts.googleapis.com">
    <link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
    <link href="https://fonts.googleapis.com/css2?family=Open+Sans:ital,wght@0,300..800;1,300..800&family=Roboto+Slab:wght@100..900&family=Ubuntu+Mono:ital,wght@0,400;0,700;1,400;1,700&display=swap" rel="stylesheet">

    <link rel="stylesheet" href="/css/normalize.css">
    <link rel="stylesheet" href="/css/main.css">
    <link rel="stylesheet" href="/css/screen.css">
    <link rel="stylesheet" href="/css/light.css">
    <link rel="stylesheet" href="/css/dark.css">

    <link rel="canonical" href="https://learnxinyminutes.com/lbstanza/">
    <script>
      localStorage.removeItem("lxiym_theme");
    </script>
  </head>
  <body>
    <div class="container">
      <div class="share">
        <span class="sharemsg">
          <a href="https://twitter.com/intent/tweet?url=https%3A%2F%2Flearnxinyminutes.com%2Flbstanza%2F&amp;text=Learn+X+in+Y+minutes%2C+where+X%3DLB+Stanza">
            Share this page
          </a>
        </span>
        <span class='st_facebook_large' displayText='Facebook'></span>
        <span class='st_twitter_large' displayText='Tweet'></span>
      </div>
      <h1><a href="/">Learn X in Y minutes</a></h1>
      <h2>Where X=LB Stanza</h2>
      <p class="filelink">
        Get the code:
        <a href="/files/learn-stanza.stanza">learn-stanza.stanza</a>
      </p>
      <div id="doc">
<p>LB Stanza (or Stanza for short) is a new optionally-typed general purpose programming language from the University of California, Berkeley. Stanza was designed to help programmers tackle the complexity of architecting large programs and significantly increase the productivity of application programmers across the entire software development life cycle.</p>
<div class="highlight"><pre>; this is a comment 
;&lt;A&gt;
This is a block comment 
    ;&lt;B&gt; 
        block comments can be nested with optional tags. 
    ;&lt;B&gt;
;&lt;A&gt;
defpackage learn-stanza-in-y: 
  import core 
  import collections 

;==============================================================================
; The basics, things you&#39;d find in most programming languages
;==============================================================================


; Variables can be mutable (var) or immutable (val)
val immutable = &quot;this string can&#39;t be changed&quot;
var mutable = &quot;this one can be&quot; 
mutable = &quot;like this&quot;

; The basic data types (annotations are optional) 
val an-int: Int = 12345
val a-long: Long = 12345L
val a-float: Float = 1.2345f
val a-double: Double = 3.14159
val a-string: String = &quot;this is a string&quot;
val a-multiline-string = \&lt;tag&gt;
    this is a &quot;raw&quot; string literal 
\&lt;tag&gt;

; Print a formatted string with println and &quot;...&quot; % [...]
println(&quot;this is a formatted string %_ %_&quot; % [mutable, immutable])

; Stanza is optionally typed, and has a ? (any) type. 
var anything:? = 0
anything = 3.14159
anything = &quot;a string&quot;

; Stanza has basic collections like Tuples, Arrays, Vectors and HashTables
val tuple: Tuple&lt;?&gt; = [mutable, immutable]

val array = Array&lt;?&gt;(3)
array[0] = &quot;string&quot;
array[1] = 1
array[2] = 1.23455
; array[3] = &quot;out-of-bounds&quot; ; arrays are bounds-checked 

val vector = Vector&lt;?&gt;()
vector[0] = &quot;string&quot;
vector[1] = 1
vector[2] = 3.14159

val hash-table = HashTable&lt;String, ?&gt;()
hash-table[&quot;0&quot;] = 0
hash-table[&quot;1&quot;] = 1 
hash-table[&quot;2&quot;] = 1 


;==============================================================================
; Functions
;==============================================================================
; Functions are declared with the `defn` keyword 
defn my-function (arg:?) : ; note the space between identifier and arg list
  println(&quot;called my-function with %_&quot; % [arg])

my-function(&quot;arg&quot;)  ; note the lack of a space to call the function

; Functions can be declared inside another function and capture variables from
; the surrounding environment.
defn outer (arg): 
  defn inner (): 
    println(&quot;outer had arg: %_&quot; % [arg])
  inner()

outer(&quot;something&quot;)

; functions are &quot;first-class&quot; in stanza, meaning you can assign variables 
; to functions and pass functions as arguments to other functions. 
val a-function = outer 
defn do-n-times (arg, func, n:Int): 
  for i in 0 to n do : 
    func(arg)
do-n-times(&quot;argument&quot;, a-function, 3)

; sometimes you want to define a function inline, or use an anonymous function.
; for this you can use the syntax: 
;   fn (args): 
;       ... 
do-n-times(&quot;hello&quot;, fn (arg): println(arg), 2)

; there is a shorthand for writing anonymous functions
do-n-times(&quot;hello&quot;, { println(_) }, 2)

; the short hand works for multiple arguments as well. 
val multi-lambda = { println(_ + 2 * _) }
multi-lambda(1, 2)

;==============================================================================
; User defined types
;==============================================================================
; Structs are declared with the `defstruct` keyword
defstruct MyStruct: 
  field

; constructors are derived automatically
val my-struct = MyStruct(&quot;field:value&quot;)

; fields are accessed using function-call syntax
println(field(my-struct))

; Stanza supports subtyping with a &quot;multimethod&quot; system based on method 
; overloading.
deftype MyType
defmulti a-method (m:MyType)

defstruct Foo &lt;: MyType
defstruct Bar &lt;: MyType
defmethod a-method (a-foo: Foo):
  println(&quot;called a-method on a Foo&quot;)

defmethod a-method (a-foo: Bar):
  println(&quot;called a-method on a Bar&quot;)

;==============================================================================
; The Type System
;==============================================================================
; True and Falseare types with a single value. 
val a-true: True = true 
val a-false: False = false 

; You can declare a union type, or a value that is one of a set of types 
val a-boolean: True|False = true 
val another-boolean: True|False = false 

; You can pattern match on types 
match(a-boolean):
  (t:True): println(&quot;is true&quot;)
  (f:False): println(&quot;is false&quot;)

; You can match against a single possible type
match(a-boolean:True):
  println(&quot;is still true&quot;)
else: 
  println(&quot;is not true&quot;)

; You can compose program logic around the type of a variable
if anything is Float :
  println(&quot;anything is a float&quot;)
else if anything is-not String : 
  println(&quot;anything is not an int&quot;)
else : 
  println(&quot;I don&#39;t know what anything is&quot;)

;==============================================================================
; Control Flow 
;==============================================================================
; stanza has the standard basic control flow 
val condition = [false, false]
if condition[0] : 
  ; do something 
  false 
else if condition[1] : 
  ; do another thing
  false 
else :
  ; whatever else
  false

; there is also a switch statement, which can be used to pattern match
; on values (as opposed to types)
switch(anything):
  &quot;this&quot;: false 
  &quot;that&quot;: false 
  &quot;the-other-thing&quot;: false 
  else: false 

; for and while loops are supported
while condition[0]: 
  println(&quot;do stuff&quot;)

for i in 0 to 10 do:  
  vector[i] = i

; stanza also supports named labels which can function as break or return 
; statements
defn another-fn (): 
  label&lt;False&gt; return:
    label&lt;False&gt; break:
      while true: 
        if condition[0] is False: 
            break(false) 
    return(false)

; For a comprehensive guide on Stanza&#39;s advanced control flow, check out 
; this page: http://lbstanza.org/chapter9.html from Stanza-by-Example

;==============================================================================
; Sequences 
;==============================================================================
; for &quot;loops&quot; are sugar for a more powerful syntax. 
val xs = [1, 2, 3] 
val ys = [&#39;a&#39;, &#39;b&#39;, &#39;c&#39;]
val zs = [&quot;foo&quot;, &quot;bar&quot;, &quot;baz&quot;]

for (x in xs, y in ys, z in zs) do : 
  println(&quot;x:%_, y:%_, z:%_&quot; % [x, y, z])


;xs, ys, and zs are all &quot;Seqable&quot; meaning they are Seq types (sequences). 
; the `do` identifier is a special function that just applies the body of
; the for loop to each element of the sequence.
; 
; A common sequence task is concatenating sequences. This is accomplished 
; using the `seq-cat` function. This is analogous to &quot;flattening&quot; iterateors
val concat = to-tuple $ 
  for sequence in [xs, ys, zs] seq-cat: 
    sequence

; we can also use a variation to interleave the elements of multiple sequences
val interleaved = to-tuple $ 
  for (x in xs, y in ys, z in zs) seq-cat : 
    [x, y, z]

println(&quot;[%,] [%,]&quot; % [concat, interleaved])

; Another common task is mapping a sequence to another, for example multiplying
; all the elements of a list of numbers by a constant. To do this we use `seq`. 
var numbers = [1.0, 2.0, 3.0, 4.0] 
numbers = to-tuple $ 
  for n in numbers seq : 
    2.0 * n 
println(&quot;%,&quot; % [numbers])

if find({_ == 2.0}, numbers) is-not False : 
  println(&quot;found it!&quot;)

; or maybe we just want to know if there&#39;s something in a sequence 
var is-there = 
  for n in numbers any? :
    n == 2.0 

; since this is &quot;syntactic sugar&quot; we can write it explicitly using an 
; anonymous function
is-there = any?({_ == 2.0}, numbers)

; a detailed reference of the sequence library and various adaptors can 
; be found here: http://lbstanza.org/reference.html#anchor439


=========================================================================
; Documentation 
;=========================================================================
;
; Top level statements can be prefixed with the &quot;doc&quot; field which takes 
; a string value and is used to autogenerate documentation for the package. 
doc: \&lt;doc&gt;
    # Document Strings 

    ```
    val you-can = &quot;include code snippets, too&quot; 
    ```

    To render documentation as markdown (compatible with mdbook)

    ```bash 
    stanza doc source.stanza -o docs 
    ```
\&lt;doc&gt;
defn docfn () : false 
</pre></div>

        <hr>
        <p>
          Got a suggestion? A correction, perhaps? <a href="https://github.com/adambard/learnxinyminutes-docs/issues/new">Open an Issue</a> on the GitHub Repo, or make a <a href="https://github.com/adambard/learnxinyminutes-docs/edit/master/lbstanza.md">pull request</a> yourself!
        </p>
        <p class="contributed">
          Originally contributed by Mike Hilgendorf, and updated by <a href="https://github.com/adambard/learnxinyminutes-docs/blame/master/lbstanza.md">3 contributors</a>.
        </p>

        <footer>
          <a style="float: left" rel="license" href="https://creativecommons.org/licenses/by-sa/3.0/deed.en_US"><img alt="Creative Commons License" style="border-width: 0; width: 80px; height: 28px; padding-bottom: 5px;" src="https://mirrors.creativecommons.org/presskit/buttons/88x31/svg/by-sa.svg" /></a>
          <p>
            &copy; 2025
            <a href="https://github.com/m-hilgendorf">Mike Hilgendorf</a>
          </p>
        </footer>
      </div>
    </div>
  </body>
</html>