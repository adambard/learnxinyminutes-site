<!DOCTYPE html lang="bg-bg" xml:lang="bg-bg" xmlns="http://www.w3.org/1999/xhtml">
<!--[if lt IE 7]>      <html class="no-js lt-ie9 lt-ie8 lt-ie7"> <![endif]-->
<!--[if IE 7]>         <html class="no-js lt-ie9 lt-ie8"> <![endif]-->
<!--[if IE 8]>         <html class="no-js lt-ie9"> <![endif]-->
<!--[if gt IE 8]><!--> <html class="no-js"> <!--<![endif]-->
    <head>
      <!-- Google tag (gtag.js) -->
      <script async src="https://www.googletagmanager.com/gtag/js?id=G-WXC8R75DEN"></script>
      <script>
        window.dataLayer = window.dataLayer || [];
        function gtag(){dataLayer.push(arguments);}
        gtag('js', new Date());

        gtag('config', 'G-WXC8R75DEN');
      </script>
        <meta charset="utf-8">
        <meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1">
        <meta http-equiv="Content-Language" content="bg-bg">
        <!-- Use title if it's in the page YAML frontmatter -->
        <title>Learn Logtalk in Y Minutes</title>
        <meta name="description" content="">
        <meta name="viewport" content="width=device-width, initial-scale=1">

        <!-- Place favicon.ico and apple-touch-icon.png in the root directory -->

        <link rel="stylesheet" href="/css/index.css">

        <link rel="canonical" href="https://learnxinyminutes.com/docs/bg-bg/logtalk-bg/">
        <script>
            var THEME_KEY = "lxiym_theme";
            function set_theme(theme) {
                var el = document.getElementsByTagName("html")[0];
                if (!el) {
                  return;
                }

                if (theme === "dark" ) {
                    el.className = "dark";
                } else {
                    el.className = "light";
                }

                localStorage.setItem(THEME_KEY, theme);
            }

            function load_theme() {
              var theme = localStorage.getItem(THEME_KEY);
              if (theme) {
                set_theme(theme);
              }
            }

            // Attempt immediate application of html style
            load_theme();

            // Backup: do it onload. Will flash, but better than nothing.
            window.addEventListener("load", function(){
              load_theme();
            });
        </script>
    </head>
    <body>
        <!--[if lt IE 7]>
            <p class="chromeframe">You are using an outdated browser. <a href="http://browsehappy.com/">Upgrade your browser today</a> or <a href="http://www.google.com/chromeframe/?redirect=true">install Google Chrome Frame</a> to better experience this site.</p>
        <![endif]-->

        <div class="container">
            <div class="share">
    <span class="sharemsg">
      <a href="https://twitter.com/intent/tweet?url=https%3A%2F%2Flearnxinyminutes.com%2Fdocs%2Fbg-bg%2Flogtalk-bg%2F&text=Learn+X+in+Y+minutes%2C+where+X%3DLogtalk">
        Share this page
      </a></span>

      <span class='st_facebook_large' displayText='Facebook'></span>
      <span class='st_twitter_large' displayText='Tweet'></span>
  </div>
  <div class="theme-choice">
    <label id="theme-label">Select theme:</label>
    <button type="button" aria-labelledby="theme-label" onclick="set_theme('light');">light</button>
    <button type="button" aria-labelledby="theme-label" onclick="set_theme('dark');">dark</button>
  </div>
  <h1><a href="/">Learn X in Y minutes</a></h1>
  <h2>Where X=Logtalk</h2>
    <p class="filelink">
    Get the code:
    <a href="/docs/files/llearnlogtalk-bg.lgt">llearnlogtalk-bg.lgt</a>
    </p>
  <div id="doc">
    <p>Logtalk е обектно-ориентиран (ОО) модерен логически език за програмиране, които разширява Prolog с възможности за капсулиране (еncapsulation) и многократно използване на кода без да компрометира декларативните възможности на езика. Logtalk е имплементиран така че да може да бъде адапртиран към всеки стандартен Prolog като back-end компилатор, тоест е напълно прозрачен за нормална Prolog програма.
Допълнително, Logtalk също може да интерпретира Prolog модули, като Logtalk обекти.</p>

<p>Основната структурна единица за изграждане на програмни със Logtalk е чрез използване на обекти.
Logtalk поддържа както стандартния начин за изграждане на иерархий от класове познати ни от езици като Java, същто така и prototype-OOP познат ни от езици като JavaScript.
Запомнете че всичко стартира с дефинирането и създаването на обект.</p>

<h2>Syntax (Синтакс)</h2>

<p>Logtalk използва стандартен Prolog синтакс, с минимум допълнителни оператори и директиви.
Важно последствие от това е че кода лесно се капсулира с много малко промени спрямо оригинален код.</p>

<p>Операторите които Logtalk добавя към Prolog са :</p>
<div class="highlight"><pre class="highlight plaintext"><code>::/2 - изпраща саобщение до обект (аналогично на метод в стандартните ООП езици)
::/1 - изпраща саобщение до себе си (self) (тоест до обекта който е получил съобщението което обработваме в момента)
^^/1 - super call (изпраща саобщение до наследен или импортиран предикат(predicate))
</code></pre></div>
<h2>Entities and roles (Субекти и роли)</h2>

<p>Logtalk предоставя обекти, портоколи и категории като първокласни-субекти (first-class entities). Връзката между тях описва ролята която субектите изпалняват. 
Обектите могат да играят различни роли в зависимост от как ги дефинираме тоест какви директиви използваме при дефиницията.</p>

<p>Например когато използваме обект А за да създадем нов обект Б, обект Б играе ролята на &ldquo;инстанция&rdquo;, а обект А играе ролята на клас.
Ако използваме &ldquo;extends&rdquo;-дефиниция единия от обектите играе ролята на протоип(prototype) за другия.</p>

<h2>Defining an object (Дефиниране на обект)</h2>

<p>Чрез дефинирането на обект ние капсулираме дефиницията на &ldquo;предикатите&rdquo;.
Обекти могат да се създадат динамично или дефинират статично във код-файла.
Ето как дефинираме примерен обект :</p>
<div class="highlight"><pre class="highlight plaintext"><code>:- object(list).

    :- public(member/2).
    member(Head, [Head| _]).
    member(Head, [_| Tail]) :-
        member(Head, Tail).

:- end_object.
</code></pre></div>
<h2>Compiling source files (Компилиран)</h2>

<p>Ако предположим че кода е записан във файл с име list.lgt, можем да го компилираме чрез logtalk_load/1 предиката или съкратения вариант {}/1.</p>
<div class="highlight"><pre class="highlight plaintext"><code>?- {list}.
yes
</code></pre></div>
<h2>Sending a message to an object (Изпращане на събщение до обект)</h2>

<p>Както казахме ::/2 infix оператор се използва за изпращане на съобщение до обекта. Както в Prolog, ние можем да backtrack-нем за алтернативни решения, понеже метода е просто стандартен предикат :</p>
<div class="highlight"><pre class="highlight plaintext"><code>?- list::member(X, [1,2,3]).
X = 1 ;
X = 2 ;
X = 3
yes

?- write_canonical(list::member(X, [1,2,3])).
::(list,member(_,[1,2,3]))
</code></pre></div>
<p>Кагато декларирме обект автоматично предикатите са капсулирани (еncapsulation), тоест извън обекта те са невидими за останалата част от програмата. Естествено има опции да променим това поведение чрез public, protected, или private предикати. </p>
<div class="highlight"><pre class="highlight plaintext"><code>:- object(scopes).

    :- private(bar/0).
    bar.

    local.

:- end_object.
</code></pre></div>
<p>Ако кода е записан в scopes.lgt фаил и се опитаме да изпратим саобщтение до частен(private) или локален предикат ще получим грешка:</p>
<div class="highlight"><pre class="highlight plaintext"><code>?- {scopes}.
yes

?- catch(scopes::bar, Error, true).
Error = error(
    permission_error(access, private_predicate, bar/0),
    logtalk(scopes::bar, user)
)
yes

?- catch(scopes::local, Error, true).
Error = error(
    existence_error(predicate_declaration, local/0),
    logtalk(scopes::local, user)
)
yes
</code></pre></div>
<p>Когато предиката е непознат за обекта това също генерира грешка. Например :</p>
<div class="highlight"><pre class="highlight plaintext"><code>?- catch(scopes::unknown, Error, true).
Error = error(
    existence_error(predicate_declaration, unknown/0),
    logtalk(scopes::unknown, user)
)
yes
</code></pre></div>
<h2>Протоколи (Defining and implementing a protocol)</h2>

<p>За тези от вас свикнали със стандартно ООП, Protocols наподобяват Interfaces в Java.
Протоколите съдържат предикати които могат да бъдат в последствие имплементирани в обекти и категории :</p>
<div class="highlight"><pre class="highlight plaintext"><code>:- protocol(listp).

    :- public(member/2).

:- end_protocol.

:- object(list,
    implements(listp)).

    member(Head, [Head| _]).
    member(Head, [_| Tail]) :-
        member(Head, Tail).

:- end_object.
</code></pre></div>
<p>Обхвата(scope) на предикатите в протокола могат да бъде ограничени чрез protected или private клаузи. 
Например:</p>
<div class="highlight"><pre class="highlight plaintext"><code>:- object(stack,
    implements(private::listp)).

:- end_object.
</code></pre></div>
<p>Всички субекти(entity) релации могат да бъдат пре-дефинирани с public, protected или private, подбно на начина показан по горе.</p>

<h2>Прототипи (Prototypes)</h2>

<p>Всеки обект без instantiation или specialization спецификация с друг обект, играе ролята на прототип. 
Прототип-обект може да предефинира и разщири протоипа-родител.</p>
<div class="highlight"><pre class="highlight plaintext"><code>% clyde, our prototypical elephant
:- object(clyde).

    :- public(color/1).
    color(grey).

    :- public(number_of_legs/1).
    number_of_legs(4).

:- end_object.

% fred, another elephant, is like clyde, except that he's white
:- object(fred,
    extends(clyde)).

    color(white).

:- end_object.
</code></pre></div>
<p>Когато системата отговаря на съобщение изпратено до обект който играе ролята на прототип, тя търси отговор първо в прототипа и ако не намери предикат делегира отговора на прототипа-родител-обект :</p>
<div class="highlight"><pre class="highlight plaintext"><code>?- fred::number_of_legs(N).
N = 4
yes

?- fred::color(C).
C = white
yes
</code></pre></div>
<p>Съобщението е валидно но няма да генерира грещка, ако предиката е дефиниран но не е деклариран/имплементиран. Това е така наречения closed-world assumption. 
Например :</p>
<div class="highlight"><pre class="highlight plaintext"><code>:- object(foo).

    :- public(bar/0).

:- end_object.
</code></pre></div>
<p>Ако заредим файла и се опитаме да извикаме bar/0, няма да получим отговор, както може да очакваме. Ако обаче предиката не е дори дефиниран, ще получим гращка :</p>
<div class="highlight"><pre class="highlight plaintext"><code>?- {foo}.
yes

?- foo::bar.
no

?- catch(foo::baz, Error, true).
Error = error(
    existence_error(predicate_declaration, baz/0),
    logtalk(foo::baz, user)
)
yes
</code></pre></div>
<h2>Класове и инстанции (Classes and instances)</h2>

<p>За да саздадем обекти които играят ролята на класове и/или инстанции, трябва да използваме поне instantiation или specialization дефиниция с друг обект. Обектите които играят роля на мета-класове могат да се използват ако е нужно още за саздаване на инстанции на класа.
Следващия пример ще илюстрира как можем динамично да саздадаваме обекти :</p>
<div class="highlight"><pre class="highlight plaintext"><code>% a simple, generic, metaclass defining a new/2 predicate for its instances
:- object(metaclass,
    instantiates(metaclass)).

    :- public(new/2).
    new(Instance, Clauses) :-
        self(Class),
        create_object(Instance, [instantiates(Class)], [], Clauses).

:- end_object.

% a simple class defining age/1 and name/1 predicate for its instances
:- object(person,
    instantiates(metaclass)).

    :- public([
        age/1, name/1
    ]).

    % a default value for age/1
    age(42).

:- end_object.

% a static instance of the class person
:- object(john,
    instantiates(person)).

    name(john).
    age(12).

:- end_object.
</code></pre></div>
<p>Когато отговаряме на съобщение изпратено до обект който играе ролята на инстанция, системата валидира съобщението първо в текущия клас, след това класа-родител ако е необходимо. Ако съобщението е валидно тогава проверяваме инстанцията :</p>
<div class="highlight"><pre class="highlight plaintext"><code>?- person::new(Instance, [name(paulo)]).
Instance = o1
yes

?- o1::name(Name).
Name = paulo
yes

?- o1::age(Age).
Age = 42
yes

?- john::age(Age).
Age = 12
yes
</code></pre></div>
<h2>Категории (Categories)</h2>

<p>Категорията е капсулран код който може да се рециклира (reuse) в различни обекти. Докато Протокола е само дефиниции Категорията е сащо и декларация/имплементация на предикатите които сме дефинирали.
В следващия пример ще дефинираме категории представящи автомобилни двигатели след което ще ги импортираме в автомобил-обекти :</p>
<div class="highlight"><pre class="highlight plaintext"><code>% a protocol describing engine characteristics
:- protocol(carenginep).

    :- public([
        reference/1,
        capacity/1,
        cylinders/1,
        horsepower_rpm/2,
        bore_stroke/2,
        fuel/1
    ]).

:- end_protocol.

% a typical engine defined as a category
:- category(classic,
    implements(carenginep)).

    reference('M180.940').
    capacity(2195).
    cylinders(6).
    horsepower_rpm(94, 4800).
    bore_stroke(80, 72.8).
    fuel(gasoline).

:- end_category.

% a souped up version of the previous engine
:- category(sport,
    extends(classic)).

    reference('M180.941').
    horsepower_rpm(HP, RPM) :-
        ^^horsepower_rpm(ClassicHP, ClassicRPM),    % "super" call
        HP is truncate(ClassicHP*1.23),
        RPM is truncate(ClassicRPM*0.762).

:- end_category.

% with engines (and other components), we may start "assembling" some cars
:- object(sedan,
    imports(classic)).

:- end_object.

:- object(coupe,
    imports(sport)).

:- end_object.
</code></pre></div>
<p>Категориите се компилират отделно и разрешават импортираните обекти да бъдат обновени като просто обновим категориите без да е необходимо да прекомпилираме обекта:</p>
<div class="highlight"><pre class="highlight plaintext"><code>?- sedan::current_predicate(Predicate).
Predicate = reference/1 ;
Predicate = capacity/1 ;
Predicate = cylinders/1 ;
Predicate = horsepower_rpm/2 ;
Predicate = bore_stroke/2 ;
Predicate = fuel/1
yes
</code></pre></div>
<h2>Hot patching</h2>

<p>Категориите още могат да се използват за промяна на обекти &ldquo;в движение&rdquo;, след като вече са били инстанциирани. 
Например :</p>
<div class="highlight"><pre class="highlight plaintext"><code>:- object(buggy).

    :- public(p/0).
    p :- write(foo).

:- end_object.
</code></pre></div>
<p>Да предположим че обекта изпечатва грешното съобщение p/0 :</p>
<div class="highlight"><pre class="highlight plaintext"><code>?- {buggy}.
yes

?- buggy::p.
foo
yes
</code></pre></div>
<p>Ако кода който описва този обект не е наличен и трябва да коригираме приложението, ние можем просто да създадем категория която да коригира необходимия предикат :</p>
<div class="highlight"><pre class="highlight plaintext"><code>:- category(patch,
    complements(buggy)).

    % fixed p/0 def
    p :- write(bar).

:- end_category.
</code></pre></div>
<p>След компилиране и зареждане на категорията ще получим :</p>
<div class="highlight"><pre class="highlight plaintext"><code>?- {patch}.
yes

?- buggy::p.
bar
yes
</code></pre></div>
<h2>Parametric objects and categories</h2>

<p>Обектите и категориите могат да се параметризират ако използваме за индентификатор комплексен-термин вместо атом.
Параметрите са логически променливи достъпни за всички капсулирани предикати. 
Пример с геометрични кръгове :</p>
<div class="highlight"><pre class="highlight plaintext"><code>:- object(circle(_Radius, _Color)).

    :- public([
        area/1, perimeter/1
    ]).

    area(Area) :-
        parameter(1, Radius),
        Area is pi*Radius*Radius.

    perimeter(Perimeter) :-
        parameter(1, Radius),
        Perimeter is 2*pi*Radius.

:- end_object.
</code></pre></div>
<p>Параметричните-обекти се използват като всеки друг обект, обикновенно осигуряваики стойности за параметрите когато изпращаме съобщение.</p>
<div class="highlight"><pre class="highlight plaintext"><code>?- circle(1.23, blue)::area(Area).
Area = 4.75291
yes
</code></pre></div>
<p>Параметричните-обекти още осигуряват лесен начин за ассоцииране на различни предикати със нормални Prolog предикати.
Prolog факти могат да бъдат интерпретирани като посредници (proxies).
Например следните клаузи на circle/2 предикат :</p>
<div class="highlight"><pre class="highlight plaintext"><code>circle(1.23, blue).
circle(3.71, yellow).
circle(0.39, green).
circle(5.74, black).
circle(8.32, cyan).
</code></pre></div>
<p>можем лесно да изчислим площа на всички кръгове :</p>
<div class="highlight"><pre class="highlight plaintext"><code>?- findall(Area, {circle(_, _)}::area(Area), Areas).
Areas = [4.75291, 43.2412, 0.477836, 103.508, 217.468]
yes
</code></pre></div>
<p>{Goal}::Message формата доказва(proves) Goal и изпраща съобщение до генерирания термин.</p>

<h2>Събития и мониторинг (Events and monitors)</h2>

<p>Logtalk поддържа event-driven програмиране чрез дефинирането на събития и монитори за тези събития.
Събитие е просто изпращане на съобщение към обект. При обработването на съобщение системата разпознава before-събитие и after-събитие.
Мониторите дефинират предикати които ще прихаванат тези събития (before/3 и after/3).
Нампример следния монитор ще прихаване съобщенията изпратени чрез ::/2  :</p>
<div class="highlight"><pre class="highlight plaintext"><code>:- object(tracer,
    implements(monitoring)).    % built-in protocol for event handlers

    :- initialization(define_events(_, _, _, _, tracer)).

    before(Object, Message, Sender) :-
        write('call: '), writeq(Object), write(' &lt;-- '), writeq(Message),
        write(' from '), writeq(Sender), nl.

    after(Object, Message, Sender) :-
        write('exit: '), writeq(Object), write(' &lt;-- '), writeq(Message),
        write(' from '), writeq(Sender), nl.

:- end_object.
</code></pre></div>
<p>Ето как можем да проследим реакцията на изпращане на съобщение :</p>
<div class="highlight"><pre class="highlight plaintext"><code>?- list::member(X, [1,2,3]).

call: list &lt;-- member(X, [1,2,3]) from user
exit: list &lt;-- member(1, [1,2,3]) from user
X = 1 ;
exit: list &lt;-- member(2, [1,2,3]) from user
X = 2 ;
exit: list &lt;-- member(3, [1,2,3]) from user
X = 3
yes
</code></pre></div>
<p>Събития могат да се изтрият динамично чрез define<em>events/5 и abolish</em>events/5 предикати.</p>

<h2>Lambda expressions</h2>

<p>Logtalk поддържа lambda expressions. Lambda параметрите се предават чрез списък към (&gt;&gt;)/2 infix оператор свързвайки ги с lambda.
Ето няколко примера :</p>
<div class="highlight"><pre class="highlight plaintext"><code>?- {library(metapredicates_loader)}.
yes

?- meta::map([X,Y]&gt;&gt;(Y is 2*X), [1,2,3], Ys).
Ys = [2,4,6]
yes
</code></pre></div>
<p>Currying се поддържа :</p>
<div class="highlight"><pre class="highlight plaintext"><code>?- meta::map([X]&gt;&gt;([Y]&gt;&gt;(Y is 2*X)), [1,2,3], Ys).
Ys = [2,4,6]
yes
</code></pre></div>
<p>Lambda free variables can be expressed using the extended syntax {Free1, &hellip;}/[Parameter1, &hellip;]&gt;&gt;Lambda.</p>

<h2>Макроси (Macros)</h2>

<p>Термини и Цели могат да бъдат пре-интерпретирани (expanded) по време на компилация ако специфицираме hook-обект който дефинира прецедурите на пре-интерпретиране.
Нека следният обект е записан във фаил source.lgt :</p>
<div class="highlight"><pre class="highlight plaintext"><code>:- object(source).

    :- public(bar/1).
    bar(X) :- foo(X).

    foo(a). foo(b). foo(c).

:- end_object.
</code></pre></div>
<p>и следния hooк-обект е записан в my_macros.lgt, който пре-интерпретира foo/1 предиката :</p>
<div class="highlight"><pre class="highlight plaintext"><code>:- object(my_macros,
    implements(expanding)).    % built-in protocol for expanding predicates

    term_expansion(foo(Char), baz(Code)) :-
        char_code(Char, Code). % standard built-in predicate

    goal_expansion(foo(X), baz(X)).

:- end_object.
</code></pre></div>
<p>След зареждането на файла с макроси ние можем да пре-интерпретираме ползайки hook-флаг за компилатора :</p>
<div class="highlight"><pre class="highlight plaintext"><code>?- logtalk_load(my_macros), logtalk_load(source, [hook(my_macros)]).
yes

?- source::bar(X).
X = 97 ;
X = 98 ;
X = 99
true
</code></pre></div>
<h2>Допълнителна информация (Further information)</h2>

<p>Посетете сайта на <a href="http://logtalk.org">Logtalk website</a> за повече информация.</p>

    <hr>
    <p>Got a suggestion? A correction, perhaps? <a href="https://github.com/adambard/learnxinyminutes-docs/issues/new">Open an Issue</a> on the Github Repo, or make a <a href="https://github.com/adambard/learnxinyminutes-docs/edit/master/bg-bg/logtalk-bg.html.markdown">pull request</a> yourself!
    </p>
    <p class="contributed">
    Originally contributed by Paulo Moura, and updated by <a href="https://github.com/adambard/learnxinyminutes-docs/blame/master/bg-bg/logtalk-bg.html.markdown">1 contributor(s)</a>.
    </p>

    <footer>
    <a style="float: left" rel="license" href="https://creativecommons.org/licenses/by-sa/3.0/deed.en_US"><img alt="Creative Commons License" style="border-width:0" src="https://i.creativecommons.org/l/by-sa/3.0/88x31.png" /></a>
    <p>
    &copy; 2024
        <a href="http://github.com/pmoura">Paulo Moura</a>
    </p>

    <p>
      Translated by:
        <a href="https://github.com/vsraptor">vsraptor</a>

    </footer>
  </div>

        </div>

        <script src="//ajax.googleapis.com/ajax/libs/jquery/1.8.0/jquery.min.js"></script>
        <script src="/js/script.js"></script>
    </body>
</html>
