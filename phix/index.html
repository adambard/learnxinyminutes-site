<!DOCTYPE html>
<html lang="en">
  <head>
    <script async src="https://www.googletagmanager.com/gtag/js?id=G-WXC8R75DEN"></script>
    <script>
      window.dataLayer = window.dataLayer || [];
      function gtag(){dataLayer.push(arguments);}
      gtag('js', new Date());

      gtag('config', 'G-WXC8R75DEN');
    </script>

    <meta charset="utf-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1">
    <meta http-equiv="Content-Language" content="en">
    <title>Learn Phix in Y Minutes</title>
    <meta name="description" content="">
    <meta name="viewport" content="width=device-width, initial-scale=1">

    <link rel="preconnect" href="https://fonts.googleapis.com">
    <link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
    <link href="https://fonts.googleapis.com/css2?family=Open+Sans:ital,wght@0,300..800;1,300..800&family=Roboto+Slab:wght@100..900&family=Ubuntu+Mono:ital,wght@0,400;0,700;1,400;1,700&display=swap" rel="stylesheet">

    <link rel="stylesheet" href="/css/normalize.css">
    <link rel="stylesheet" href="/css/main.css">
    <link rel="stylesheet" href="/css/screen.css">
    <link rel="stylesheet" href="/css/light.css">
    <link rel="stylesheet" href="/css/dark.css">

    <link rel="canonical" href="https://learnxinyminutes.com/phix/">
    <script>
      localStorage.removeItem("lxiym_theme");
    </script>
  </head>
  <body>
    <div class="container">
      <div class="share">
        <span class="sharemsg">
          <a href="https://twitter.com/intent/tweet?url=https%3A%2F%2Flearnxinyminutes.com%2Fphix%2F&amp;text=Learn+X+in+Y+minutes%2C+where+X%3DPhix">
            Share this page
          </a>
        </span>
        <span class='st_facebook_large' displayText='Facebook'></span>
        <span class='st_twitter_large' displayText='Tweet'></span>
      </div>
      <h1><a href="/">Learn X in Y minutes</a></h1>
      <h2>Where X=Phix</h2>
      <p class="filelink">
        Get the code:
        <a href="/files/learnphix.exw">learnphix.exw</a>
      </p>
      <div id="doc">
<div class="highlight"><pre>            -- single line comment

            // single line comment

            /* multi-
                     line comment */

// Start programming immediately

    -- write using UTF8; save as: hello.ex
    -- use ? for output

        ? &quot;ğŸ˜ hello , ğŸ˜ world!&quot;
        ? sqrt(2+2)

// Interpret your program
                            /*
       p hello               */

// Compile your program
                            /*
       p -c hello            */

// Coding mistakes receive gentle help messages
                                                          /*
    string line
    line = 5
           ^ type error (storing atom in string)           */

// Every literal value, constant, and variable is an &#39;&#39;object&#39;&#39;

    -- a literal object
        ? &quot;hello&quot;
        ? PI
        ? { &quot;hello&quot;, PI }

    -- a named variable object
        object X
        X = &quot;hello&quot;
        X = PI
        X = { &quot;hello&quot;, PI }

    -- a named constant object
        constant myPI = 22/7

//  Everything is an &#39;&#39;object&#39;&#39;, just two fundemental kinds
                                                                /*
            â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â–„
          â”Œâ”€â”¤   object           â–ˆâ”€â”
          â”‚ â””â”€â–„â–„â–„â–„â–„â–„â–„â–„â–„â–„â–„â–„â–„â–„â–„â–„â–„â–„â–„â–ˆ â”‚
          â”‚                        â”‚
       &quot;atom&quot;                   &quot;container&quot;                     */

       number  x = 3.14159      sequence  s   = { &quot;hello&quot;, PI }
       integer y = 3            string    txt = &quot;hello&quot;

            -- simplify,
            -- and use only two primitives
            number x1=3.14156, y1=3
            sequence s1={&quot;hello&quot;,PI}, txt1=&quot;hello&quot;

                -- simplify even more,
                -- and use just one primitive
                object x2=3.14156, y2=3, s2={&quot;hello&quot;,PI}, txt2=&quot;hello&quot;

// Elegant data-type design

    -- invent your own &quot;type&quot;
    -- organize with &quot;struct&quot; or &quot;class&quot;
                                                                /*
    â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•—
    â•‘   â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â” â•‘
    â•‘   â”‚      â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â–„      â”‚ â•‘
    â•‘   â”‚    â”Œâ”€â”¤ object    â–ˆâ”€â”    â”‚ â•‘
    â•‘   â”‚    â”‚ â””â”€â–„â–„â–„â–„â–„â–„â–„â–„â–„â–„â–ˆ â”‚    â”‚ â•‘
    â•‘   â”‚    â”‚               â”‚    â”‚ â•‘
    â•‘   â”‚  number        sequence â”‚ â•‘
    â•‘   â”‚    â”‚               â”‚    â”‚ â•‘
    â•‘   â”‚  integer        string  â”‚ â•‘
    â•‘   â””â”€â”€â”€â”€â”€â”€â”€â”€ type â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”˜ â•‘
    â•‘                               â•‘
    â•šâ•â•â•â•â•â•â•â• struct                â•‘
              class â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
                                                                */

// Syntax is consistant: &quot;keyword...end keyword&quot;

    -- no invisible syntax or extra rules needed.

    // loop
    -- while ... end while

        integer index = 1
        while index &lt;= 5 do
            ? index
            index += 1
        end while

    // loop
    -- for ... end for

        for i=5 to 1 by -1 do
            ? i
        end for

    //  conditional
    --  if ... end if

        number p = 4
        if p &lt; 1 then
            ? &quot;p is a small number&quot;
        elsif p &gt; 10 then
            ? &quot;p is a large number&quot;
        else
            ? &quot;p is inbetween&quot;
        end if

    // conditional
    -- switch ... end switch

        object ch = prompt_string(&quot;enter one character: &quot; )
        switch ch
            case &quot;a&quot;: ? &quot;ch is a&quot;
            case &quot;b&quot;: ? &quot;ch is b&quot;
            case &quot;c&quot;: ? &quot;ch is c&quot;
            default:  ? &quot;ch is something else&quot;
        end switch

// Operators are always consistant; never overloaded.

    -- the + operator &#39;&#39;always adds&#39;&#39;
        ? 2+7      --&gt; 9
        ? &#39;A&#39; + 32 --&gt; 97

    -- the &amp; operator &#39;&#39;always concatenates&#39;&#39;
        ? 2 &amp; 7                --&gt; {2,7}
        ? &quot;cat&quot; &amp; &quot; &quot; &amp; &quot;dog&quot;  --&gt; &quot;cat dog&quot;
        ? {1,2,3} &amp; &quot;fish&quot;     --&gt; {1,2,3} &amp; &quot;fish&quot;
        pp( {1,2,3} &amp; &quot;fish&quot; ) --&gt; {1,2,3,102&#39;f&#39;,105&#39;i&#39;,115&#39;s&#39;,104&#39;h&#39;}

// Use &#39;&#39;sq_&#39;&#39; functions to span entire containers.

        ? sq_add( {1,2,3}, 10 )     --&gt; {11,12,13}
        ? sq_sqrt( {4,9,16} )       --&gt; {2,3,4}

// Functions must return a value

        function add2( number x, number y )
            number sum = x + y
            return sum
        end function
        ? add2( 4, 9 )

// Procedures do not return a value

        procedure sum_all( sequence lst )
            number sum = 0
            for i=1 to length(lst) do
                sum += lst[i]
            end for
            ? sum
        end procedure
        sum_all( {1,3,9,11} )

// Recursion and mutal recursion are permitted

        function factorial(number n)
            if n == 0 then
                return 1
            end if
            if n&lt;0 then
                return &quot;error, no negative numbers for factorials&quot;
            end if
            return n * factorial(n - 1)
        end function
        ? factorial(5)

// User defined data-types

        -- defined like a function: type ... end type
        -- they are fully programmable; add your own features

        type positive( number x )
            if not integer(x) then
                ? &quot;use integers for factorials&quot;
                return False
            end if
            if x &lt; 0 then
                ? &quot;error, no negative numbers for factorials&quot;
                return False
            end if
            return True
        end type

     -- use them to declare variables and parameters

        function factorial2( positive n )
            if n == 0 then return 1 end if
            return n * factorial2(n-1)
        end function
        ? factorial(5)

    -- to catch errors, and recover, use: try ... end try

       try
          ? factorial2( -5 )
       catch e
          ? &quot;that was a mistake&quot;
       end try

// Sequences are versatile

    -- multiple assignment
            number a, b, c
            {a,b,c} = { -100, -200/-2, -300*3 }
            ? a      --&gt; -100
            ? b      --&gt;  100
            ? c      --&gt; -900

    -- swapping values
                ? a       --&gt; -100
                ? c       --&gt; -900
            {a,c} = {c,a}
                ? a       --&gt; -900
                ? c       --&gt; -100


// Symmetrical one-based indexing does it all

    -- both sequence and string are mutable and work alike

    --              1   2   3   4   5    -- index head to tail
             s = { 10, 20, 30, 40, 50 }
    --             -5  -4  -3  -2  -1    -- index tail to head

        // one item
            ? s[ 2]
            ? s[-4]
                        -- output for both is:
            -----&gt;  20

        // slice with one item
            ? s[ 2.. 2]
            ? s[-4..-4]
                        -- output for both is:
            -----&gt; {20}

        // inclusive slice
            ? s[ 2.. 4]
            ? s[-4..-2]
                        -- output for both is:
            -----&gt; {20,30,40}

        // empty sequence
            ? s[3 .. 2]
            ? s[-3..-4]
                        -- output for both is:
            -----&gt; {}

        // insert
            s[3..2] = {99}
            ? s
            -----&gt; {10,20,99,30,40,50}

        // prepend and append
            s = { 10,20,30,40,50 }

            s[  1..0] = {0}             -- prepend
            s[$+1..$] = {6}             -- append

            ? s
            -----&gt; {0,10,20,99,30,40,50,6}

            s[0..-1] = {9999}           -- append

            ? s
            -----&gt; {0,10,20,99,30,40,50,6,9999}

        // delete
            s = { 10,20,30,40,50 }

            s[2..2] = {}        -- item deleted
            ? s
            -----&gt; {10,30,40,50}

            s[2..3] = {}        -- slice deleted
            ? s
            -----&gt; {10,50}

// Learn and reuse; you keep what you learn.

            s = { 1,3,5,7 }
            txt = &quot;jello&quot;

        -- &quot;find&quot; locates one item in either a sequence or a string
        ? find( 3, s ) --&gt; 2
        ? find( &#39;e&#39;, txt ) --&gt; 2

        -- &quot;match&quot; locates a slice in either a sequence or a string
        ? match( {5,7}, s ) -- &gt; 3
        ? match( &quot;ll&quot;, txt ) --&gt; 3

// Look back at the examples, Phix is generic!

// Batteries are installed

        ? sort( {2, 54,6,4, 0} )
        ? upper( &quot;cat&quot; )
        ? log( 10.4 )
        ? trunc(1.4)         --  1
        ? floor(1.4)         --  1
        ? trunc(-1.4)        -- -1
        ? floor(-1.4)        -- -2

// Batteries are included

        include builtins/regex.e

        string str = &quot;say hello and smile&quot;
        str = gsub( `s...e`, str, &quot;ğŸ˜&quot; )
        ? str   --&gt; &quot;say hello and ğŸ˜&quot;

// Yes, sequences are &quot;powerful&quot;

        function odd(integer a) return remainder(a,2)=1 end function
        function even(integer a) return remainder(a,2)=0 end function

        ? tagset(10)                 --&gt; {1,2,3,4,5,6,7,8,9,10}
        ? filter(tagset(10),odd)     --&gt; {1,3,5,7,9}
        ? filter(tagset(10),even)    --&gt; {2,4,6,8,10}

// A &#39;&#39;struct&#39;&#39; provides named fields, type-checking, and dot notation

        struct point
            number x = 0
            number y = 0
        end struct

        procedure show( point q )
            printf(1, &quot;(%g,%g)&quot;, { q.x, q.y } )
        end procedure

        point  p1 = new()
        show(p1)
            --&gt; (0,0)

        p1.x = 3
        p1.y = 5
        show( p1 )
            --&gt; (3,5)

// A &#39;&#39;class&#39;&#39; adds methods and scope control

        class pair
            public number x = 0
            public number y = 0

            procedure show( )
                printf(1, &quot;(%g,%g)&quot;, { this.x, this.y } )
            end procedure
        end class

        pair  p2 = new()
        p2.show()
            --&gt; (0,0)

        p2.x = 3
        p2.y = 5
        p2.show()
            --&gt; (3,5)

// Inherit and compose

        class Pair -- any 2 objects
            public sequence xy
            public integer x,y
                 function get_x()
                  return xy[1]
                 end function

                 function get_y()
                  return xy[2]
                 end function
        end class

        type pos_seq(sequence x)
            return min(x) &gt;= 0
        end type

        class Point extends Pair
            public pos_seq loc -- any two numbers &gt;= 0

                procedure set_loc(object x)
                    this.xy = {x[1],x[2]}
                end procedure
        end class

        class Rectangle extends Point
            public Point tlc,brc --top_left, bottom_right corners;
            public sequence size

                  function get_size()
                    this.size = {brc.x-tlc.x , brc.y-tlc.y}
                    return this.size
                  end function
        end class

        Point p1a = new() p1a.loc = {50,10}
        Point p2a = new() p2a.loc = {300,200}

        Rectangle r = new()
                  r.tlc = p1a
                  r.brc = p2a
        ? r          -- {&quot;struct&quot;,&quot;Rectangle&quot;,4,1}
        ? r.tlc      -- {&quot;struct&quot;,&quot;Point&quot;,3,3}

        ? r.size       --&gt; {250,190}
        ? r.get_size() --&gt; {250,190}
</pre></div>
<p>Phix does not (although most can be emulated) directly support
operator|builtin|function overloading, lambda expressions, closures,
currying, eval, partial function application, function composition,
function prototyping, monads, generators, anonymous recursion,
the Y combinator, aspect oriented programming, interfaces, delegates,
first class environments, implicit type conversion
(of the destructive kind), interactive programming, inverted syntax,
list comprehensions, metaprogramming, pointers
(other than to raw allocated memory), topic variables,
enforced singletons, safe mode, s-expressions,
or formal proof construction.</p>
<p>The author wryly comments:</p>
<p>''That should both scare off and attract the right people''.</p>
<h2>References</h2>
<ul>
<li><a href="http://phix.x10.mx">http://phix.x10.mx</a></li>
<li><a href="https://github.com/petelomax/Phix">Source code</a></li>
<li><a href="https://openeuphoria.org/forum/index.wc">Forum</a></li>
<li><a href="https://rosettacode.org/wiki/Category:Phix">Rosetta Code</a></li>
</ul>

        <hr>
        <p>
          Got a suggestion? A correction, perhaps? <a href="https://github.com/adambard/learnxinyminutes-docs/issues/new">Open an Issue</a> on the GitHub Repo, or make a <a href="https://github.com/adambard/learnxinyminutes-docs/edit/master/phix.md">pull request</a> yourself!
        </p>
        <p class="contributed">
          Originally contributed by pxtom, and updated by <a href="https://github.com/adambard/learnxinyminutes-docs/blame/master/phix.md">3 contributors</a>.
        </p>

        <footer>
          <a style="float: left" rel="license" href="https://creativecommons.org/licenses/by-sa/3.0/deed.en_US"><img alt="Creative Commons License" style="border-width: 0; width: 88px; height: 31px;" src="https://mirrors.creativecommons.org/presskit/buttons/88x31/svg/by-sa.svg" /></a>
          <p>
            &copy; 2025
            <a href="https://gitgub.com/pxtom">pxtom</a>
          </p>
        </footer>
      </div>
    </div>
  </body>
</html>